plugins {
    id 'java'
    id 'maven-publish'
}

group = 'fr.atlasworld.contentwork.api'
version = "${project_version}"

repositories {
    mavenCentral()
    maven {
        name 'PaperMC Repo'
        url 'https://repo.papermc.io/repository/maven-public/'
    }
}

dependencies {
    compileOnly "io.papermc.paper:paper-api:1.20.1-R0.1-SNAPSHOT"
}

jar {
    archiveBaseName = 'ContentWork-api'
}

tasks.register('sourceJar', Jar) {
    group 'build'
    from sourceSets.main.allSource
    archiveClassifier.set('sources')
    archiveBaseName = 'ContentWork-api'
}

assemble {
    dependsOn(sourceJar)
}

publishing {
    publications {
        mavenJava(MavenPublication) {

            groupId = 'fr.atlasworld'
            artifactId = 'contentwork-api'
            version = project.version

            from components.java
            pom {
                name = 'ContentWork'
                description = 'PaperMC API to easily add item, blocks and entity to the game '
                url = 'https://atlasworld.fr/'
                licenses {
                    license {
                        name = 'MIT'
                        url = 'https://github.com/AtlasWorldMC/ContentWork/blob/main/LICENSE'
                    }
                }
            }

            // Attach the sources jar
            artifact sourceJar
        }
    }
    repositories {
        maven {
            name 'AtlasWorld-Releases'
            url 'https://maven.atlasworld.fr/repository/maven-releases/'
            credentials {
                username = "$maven_user"
                password = "$maven_password"
            }
        }
    }
}